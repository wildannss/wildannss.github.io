VERSION BUILD=8920312 RECORDER=FX
SET !EXTRACT_TEST_POPUP NO
SET !ERRORIGNORE NO
SET !TIMEOUT_PAGE 999
SET !TIMEOUT_STEP 999
TAB CLOSEALLOTHERS
'
' This imacros script created by 9kw.eu
' Find more here: http://www.9kw.eu/
'
' The script fill the captcha of confident demo.
'
' Note for users with firefox with the message "Firefox prevented this page from automatically reloading.":
' UNCHECK the option "warn me when pages try to redirect" in your browser
' You find the checkbox under Options > Advanced > General > Accessibility

' Path to the captcha image with timestamp
' Tempfolder like C:\ or C:\TEMP\ under windows or like /tmp/ under linux
SET tempfolder C:\

' Tempslash (Path with slashs as tempfolder) and with the htmlfile confident.html
SET tempslash C:/

' Tempfile
SET tempfile captcha_{{!NOW:yyyymmdd_hhnnss}}.jpg

' Step 1: Config for 9kw.eu for your apikey
SET apikey your_api_key
'And priority (prio 1-20)
SET prio 0

' Step 2: Save the captcha picture to local disk
TAB T=1
'URL GOTO=http://confidenttechnologies.com/demos/confident-captcha-demo/
URL GOTO=http://confidenttechnologies.com/confident-captcha-demo/
'EVENT TYPE=CLICK SELECTOR="#confidentcaptcha_badge_image>IMG" BUTTON=0
EVENT TYPE=CLICK SELECTOR="#confidentcaptcha_badge_grid>IMG" BUTTON=0
WAIT SECONDS=3
ONDOWNLOAD FOLDER={{tempfolder}} FILE={{tempfile}} WAIT=YES

SEARCH SOURCE=REGEXP:"Type in the 3 letters you see on pictures of the(.*)in that order" IGNORE_CASE=YES EXTRACT="$1"
SET confidentselect {{!EXTRACT}}
SET !EXTRACT NULL
SET confidentselect1 EVAL("var s=\"{{confidentselect}}\"; var mySplitResult = s.split('<span class=\"category_name\">');mySplitResult[1].split('</span>')[0];")
SET confidentselect2 EVAL("var s=\"{{confidentselect}}\"; var mySplitResult = s.split('<span class=\"category_name\">');mySplitResult[2].split('</span>')[0];")
SET confidentselect3 EVAL("var s=\"{{confidentselect}}\"; var mySplitResult = s.split('<span class=\"category_name\">');mySplitResult[3].split('</span>')[0];")
WAIT SECONDS=5
TAG POS=1 TYPE=DIV ATTR=CLASS:confidentcaptcha_anchor CONTENT=EVENT:SAVE_ELEMENT_SCREENSHOT
WAIT SECONDS=5

TAB OPEN
TAB T=2
URL GOTO=file:///{{tempslash}}confident.html?file:///{{tempslash}}{{tempfile}}
WAIT SECONDS=5
TAG POS=1 TYPE=INPUT ATTR=ID:nextthing CONTENT="1. {{confidentselect1}}, 2. {{confidentselect2}}, 3. {{confidentselect3}}"
FILEDELETE NAME={{tempfolder}}{{tempfile}}
TAG POS=1 TYPE=DIV ATTR=* CONTENT=EVENT:SAVE_ELEMENT_SCREENSHOT
TAB CLOSE


'Wait a random number (1 to 5) of seconds
SET randomnumber EVAL("Math.floor(Math.random()*5 + 1);")
WAIT SECONDS={{randomnumber}}

'Syntaxcheck: API Key, prio
SET apikey EVAL("var s=\"{{apikey}}\"; if(s.match(/^[a-zA-Z0-9]+$/) && s.length <= 50 && s.length >= 5) s; else MacroError(\"API Key is wrong.\")")
SET prio EVAL("var s=\"{{prio}}\", d = parseFloat(s); if(d >= 0 && d <= 20) d; else MacroError(\"Value(Prio) is not in the set range.\")")

'Step 3: Open the a new tab, and go to 9kw.eu, and submit the captcha picture
TAB OPEN
TAB T=2
URL GOTO=http://www.9kw.eu/grafik/form.html
'The apikey is used to identify each of our customers, which you can get from the our page. It is assigned to the CONTENT.
TAG POS=1 TYPE=INPUT ATTR=NAME:apikey CONTENT={{apikey}}
'Priority in our system like min. 0 to max. 20 (cost +0-20)
TAG POS=1 TYPE=INPUT ATTR=NAME:prio CONTENT={{prio}}
'Options for the form. See more under http://www.9kw.eu/grafik/form.html and http://www.9kw.eu/api.html
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:selfsolve CONTENT=NO
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:confirm CONTENT=NO
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:case-sensitive CONTENT=NO
'TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:nomd5 CONTENT=YES
'We need only numbers for this captcha
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:numeric CONTENT=YES
TAG POS=1 TYPE=INPUT ATTR=NAME:source CONTENT=imacros
'The path of the captcha picture saved is assigned to the CONTENT
TAG POS=1 TYPE=INPUT ATTR=NAME:file-upload-01 CONTENT={{tempfolder}}{{tempfile}}
'Submit the formdata to 9kw.eu
TAG POS=1 TYPE=INPUT ATTR=TYPE:submit
'Extract the captchaid from your captcha submit
TAG POS=1 TYPE=INPUT ATTR=NAME:captchaid EXTRACT=TXT
SET captchaid {{!EXTRACT}}
'Clean the !EXTRACT variable for the next task
SET !EXTRACT NULL
'Extract the characters that are recoginzed from the picture of captcha.
TAG POS=1 TYPE=INPUT ATTR=NAME:result EXTRACT=TXT
'Step 4: Check the captcha answer (text or nothing like #EANF# = Extraction Anchor Not Found)
SET answer EVAL("if (\"{{!EXTRACT}}\" == \"#EANF#\") {var x = \"\";} else {var x = \"{{!EXTRACT}}\";} x;")
'Clean the !EXTRACT variable for the next task
SET !EXTRACT NULL
TAB CLOSE

'Display extracted data (only for debug)
'PROMPT {{captchaid}}

'Step 5: Fill the recognized characters to the verification box (Click the pictures 1..3)
TAB T=1
SET !ERRORIGNORE YES
SET n1 EVAL("var s=\"{{answer}}\"; if(s.match(/^[0-9]+$/) && s.length <= 3 && s.length >= 2) s.substr(0,1); else MacroError(\"Captcha answer is wrong.\")")
TAG POS={{n1}} TYPE=DIV ATTR=CLASS:confidentcaptcha_image_highlight&&TXT:
SET n2 EVAL("var s=\"{{answer}}\"; if(s.match(/^[0-9]+$/) && s.length <= 3 && s.length >= 2) s.substr(1,1); else MacroError(\"Captcha answer is wrong.\")")
TAG POS={{n2}} TYPE=DIV ATTR=CLASS:confidentcaptcha_image_highlight&&TXT:
SET n3 EVAL("var s=\"{{answer}}\"; if(s.match(/^[0-9]+$/) && s.length <= 3 && s.length >= 2) s.substr(2,1); else MacroError(\"Captcha answer is wrong.\")")
TAG POS={{n3}} TYPE=DIV ATTR=CLASS:confidentcaptcha_image_highlight&&TXT:
SET !ERRORIGNORE NO

'Step 6: Check and send the captcha feedback back to the captcha service (OK:1, NotOK:2, EN: Right/False, DE: Richtig/Falsch)
SEARCH SOURCE=REGEXP:"(Thank you)" IGNORE_CASE=YES EXTRACT="$1"
SET htmlcode {{!EXTRACT}}
SET !EXTRACT NULL
SET feedback EVAL("if (\"{{htmlcode}}\" == \"Thank you\") {var x = \"1\";} else {var x = \"2\";} x;")
TAB OPEN
TAB T=2
URL GOTO=http://www.9kw.eu/index.cgi?source=imacros&action=usercaptchacorrectback&apikey={{apikey}}&correct={{feedback}}&id={{captchaid}}
WAIT SECONDS=2
TAB CLOSE
TAB T=1

'Cleanup: Delete the old captcha picture
FILEDELETE NAME={{tempfolder}}{{tempfile}}