VERSION BUILD=8820413
SET !EXTRACT_TEST_POPUP NO
SET !ERRORIGNORE NO
SET !TIMEOUT_PAGE 999
SET !TIMEOUT_STEP 999
TAB CLOSEALLOTHERS
'
' This imacros script created by 9kw.eu
' Find more here: http://www.9kw.eu/
'
' The script fill the captcha of recaptcha demo.
'
' Note for users with firefox with the message "Firefox prevented this page from automatically reloading.":
' UNCHECK the option "warn me when pages try to redirect" in your browser
' You find the checkbox under Options > Advanced > General > Accessibility

' Path to the captcha image with timestamp
' Tempfolder like C:\ or C:\TEMP under windows or like /tmp under linux
SET !VAR0 C:\
' Tempfile
SET !VAR1 captcha_{{!NOW:yyyymmdd_hhnnss}}.jpg

' Step 1: Config for 9kw.eu for your apikey
SET !VAR2 your_api_key
'And priority (prio 1-20)
SET !VAR3 0

' Step 2: Save the captcha picture to local disk
TAB T=1
URL GOTO=https://www.google.com/recaptcha/demo/ajax
TAG POS=1 TYPE=INPUT:BUTTON FORM=NAME:NoFormName ATTR=*
ONDOWNLOAD FOLDER={{!VAR0}} FILE={{!VAR1}} WAIT=YES
TAG POS=1 TYPE=IMG ATTR=ID:recaptcha_challenge_image CONTENT=EVENT:SAVEPICTUREAS

'Wait a random number (1 to 5) of seconds
SET !VAR9 EVAL("Math.floor(Math.random()*5 + 1);")
WAIT SECONDS={{!VAR9}}

'Syntaxcheck: API Key, prio
SET !VAR2 EVAL("var s=\"{{!VAR2}}\"; if(s.match(/^[a-zA-Z0-9]+$/) && s.length <= 50 && s.length >= 5) s; else MacroError(\"API Key is wrong.\")")
SET !VAR3 EVAL("var s=\"{{!VAR3}}\", d = parseFloat(s); if(d >= 0 && d <= 20) d; else MacroError(\"Value(Prio) is not in the set range.\")")

'Step 3: Open the a new tab, and go to 9kw.eu, and submit the captcha picture
TAB OPEN
TAB T=2
URL GOTO=http://www.9kw.eu/grafik/form.html
'The apikey is used to identify each of our customers, which you can get from the our page. It is assigned to the CONTENT.
TAG POS=1 TYPE=INPUT ATTR=NAME:apikey CONTENT={{!VAR2}}
'Priority in our system like min. 0 to max. 20 (cost +0-20)
TAG POS=1 TYPE=INPUT ATTR=NAME:prio CONTENT={{!VAR3}}
'Options for the form. See more under http://www.9kw.eu/grafik/form.html and http://www.9kw.eu/api.html
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:selfsolve CONTENT=NO
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:confirm CONTENT=NO
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:case-sensitive CONTENT=NO
'TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ACTION:/index.cgi ATTR=NAME:nomd5 CONTENT=YES
TAG POS=1 TYPE=INPUT ATTR=NAME:source CONTENT=imacros
'The path of the captcha picture saved is assigned to the CONTENT
TAG POS=1 TYPE=INPUT ATTR=NAME:file-upload-01 CONTENT={{!VAR0}}{{!VAR1}}
'Submit the formdata to 9kw.eu
TAG POS=1 TYPE=INPUT ATTR=TYPE:submit
'Extract the captchaid from your captcha submit
TAG POS=1 TYPE=INPUT ATTR=NAME:captchaid EXTRACT=TXT
SET !VAR5 {{!EXTRACT}}
'Clean the !EXTRACT variable for the next task
SET !EXTRACT NULL
'Extract the characters that are recoginzed from the picture of captcha.
TAG POS=1 TYPE=INPUT ATTR=NAME:result EXTRACT=TXT
TAB CLOSE
TAB T=1

'Display extracted data (only for debug)
'PROMPT {{!VAR5}}

'Step 4: Check the captcha answer (text or nothing like #EANF# = Extraction Anchor Not Found)
SET !VAR6 EVAL("if (\"{{!EXTRACT}}\" == \"#EANF#\") {var x = \"\";} else {var x = \"{{!EXTRACT}}\";} x;")
'Clean the !EXTRACT variable for the next task
SET !EXTRACT NULL

'Step 5: Fill the recognized characters to the verification box
TAG POS=1 TYPE=INPUT:TEXT FORM=NAME:NoFormName ATTR=ID:recaptcha_response_field CONTENT={{!VAR6}}
TAG POS=1 TYPE=INPUT:SUBMIT FORM=NAME:NoFormName ATTR=NAME:Button1
WAIT SECONDS=2

'Step 6: Check and send the captcha feedback back to the captcha service (OK:1, NotOK:2, EN: Right/False, DE: Richtig/Falsch)
SEARCH SOURCE=REGEXP:"(Right|Richtig|Falsch|False|Wrong|Correct|Incorrect)" IGNORE_CASE=YES EXTRACT="$1"
SET !VAR7 {{!EXTRACT}}
SET !EXTRACT NULL
SET !VAR8 EVAL("if (\"{{!VAR7}}\" == \"Richtig\" || \"{{!VAR7}}\" == \"Right\" || \"{{!VAR7}}\" == \"Correct\") {var x = \"1\";} else {var x = \"2\";} x;")
TAB OPEN
TAB T=2
URL GOTO=http://www.9kw.eu/index.cgi?source=imacros&action=usercaptchacorrectback&apikey={{!VAR2}}&correct={{!VAR8}}&id={{!VAR5}}
WAIT SECONDS=2
TAB CLOSE
TAB T=1

'Cleanup: Delete the old captcha picture
FILEDELETE NAME={{!VAR0}}{{!VAR1}}